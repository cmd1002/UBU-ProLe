options {
    STATIC=false;
	DEBUG_PARSER=false;
}

PARSER_BEGIN(Compiler)

/**
 * Language parser class.
 *
 * @author <a href="mailto:rdg1003@alu.ubu.es">Rodrigo Díaz García</a>
 */
public class Compiler {
    /**
     * Main method. Parses either stdin or a file.
     *
     * @param args if empty, the parser will parse stdin. Otherwise, it will parse the file in the first argument. 
     */
    public static void main(String args[]) {
        try {
            final Compiler compiler = initCompiler(args);
            compiler.program();
        } catch (java.io.FileNotFoundException ex) {
            System.out.println("File not found (" + args[0] + "), first argument must be a valid path.");
        } catch (ParseException ex) {
            System.out.println(ex);
            System.out.println("Parser caught an unexpected exception, exiting...");
        }
    }

    /**
     * Creates the compiler according to the given arguments.
     *
     * @param args if empty, the parser will parse stdin. Otherwise, it will parse the file in the first argument.
     * @return a Compiler instance.
     */
    static Compiler initCompiler(String args[]) throws java.io.FileNotFoundException {
        if (args.length < 1) {
            return new Compiler(System.in);
        }
        return new Compiler(new java.io.FileInputStream(args[0]));
    }

    /**
     * The current label.
     */
    public static int labelCount = 0;
    
    /**
     * Get the next label.
     *
     * @return the latest label.
     */
    public static int getNewLabel() {
        return Compiler.labelCount++;
    }
}

PARSER_END(Compiler)

TOKEN : {
    <NUMBER: (["0"-"9"])+>
|   <#ALPHA: ["a"-"z", "A"-"Z"]>
|   <END: "end">
|   <THEN: "then">
|   <DO: "do">
|   <ELSE: "else">
|   <IF: "if">
|   <ELIF: "elsif">
|   <UNLESS: "unless">
|   <UNTIL: "until">
|   <WHILE: "while">
|   <PRINT: "print">
|   <EOL: ";">
|   <EQUALS: "=">
|   <ADDITION: "+">
|   <ADDITION_EQUALS: "+=">
|   <SUBSTRACTION: "-">
|   <SUBSTRACTION_EQUALS: "-=">
|   <MULTIPLICATION: "*">
|   <MULTIPLICATION_EQUALS: "*=">
|   <DIVISION: "/">
|   <DIVISION_EQUALS: "/=">
|   <PARENTHESIS_START: "(">
|   <PARENTHESIS_END: ")">
|   <RESERVED: "class" | "ensure" | "nil" | "self" | "while" | "alias" | "defined" | "for" | "or" | "then" | "elsif" | "do" | "else" | "begin" | "unless" | "until" | "end" | "return" | "next" | "if" | "print">
|   <VARIABLE: (<ALPHA> | "_") (<ALPHA> | <NUMBER> | "_")*>
}

<*>SKIP : {
    <WHITESPACE: "\n" | "\r" | "\t" | " ">
|   <COM_LINE: "#" (~["\n", "\r"])*>
|   <COM_MULTI_START: "=begin" > : COM_MULTI
}

<COM_MULTI>SKIP : {
    <COM_MULTI_END: "=end"> : DEFAULT
|   <COM_MULTI_LONGEST: (~["="])+>
|   <COM_MULTI_OTHER: ~[]>
}


void program() : {} {
    statement() (";" program())?
}

void statement() : {} {
    primmary() | statementAssigment()
}

void statementAssigment() : {
    Token leftValue, rightValue;
    String operation;
    boolean eq, eq2;
} {
    leftValue = <VARIABLE> {System.out.println("\tvalori " + leftValue.image);}
    operation = assigment(leftValue)
    expression() {
        if (operation != null) {
            System.out.println('\t' + operation);
        }
        System.out.println("\tasigna");
    }
}

String assigment(Token leftValue) : {} {
    <EQUALS> { return null; }
|   <ADDITION_EQUALS> {
        System.out.println("\tvalord " + leftValue.image);
        return "sum";
    }
|   <SUBSTRACTION_EQUALS> {
        System.out.println("\tvalord " + leftValue.image);
        return "sub";
    }
|   <MULTIPLICATION_EQUALS> {
        System.out.println("\tvalord " + leftValue.image);
        return "mul";
    }
|   <DIVISION_EQUALS> { 
        System.out.println("\tvalord" + leftValue.image);
        return "div";
    }
}

void primmary() : {} {
    primmaryIf()
|   primmaryUnless()
|   primmaryUntil()
|   primmaryWhile()
|   primmaryPrint()
}

void primmaryThen() : {} {
    <THEN> program()
}

int primmaryDo(boolean condition) : {
    String conditionToString = condition ? "cierto" : "falso";
    int label;
} {
    <DO> {
        label = getNewLabel();
        System.out.println("\tsi" + conditionToString + "vea LBL" + label);
    }
    program()
    { return label; }
}

void primmaryElse(int lastLabel) : {} {
    <ELSE> {
        System.out.println("\tvea LBL" + lastLabel);
        System.out.println("LBL" + (lastLabel - 1));
    }
    program() { System.out.println("LBL" + getNewLabel()); }
}

void primmaryIf() : {
    int firstLabel, lastLabel;
} {
    <IF>
    expression() {
        lastLabel = firstLabel = getNewLabel();
        System.out.println("\tsifalsovea LBL" + lastLabel);
    }
    primmaryThen()
    (lastLabel = primmaryElseIf(lastLabel))*
    (primmaryElse(lastLabel))?
    <END> { System.out.println("LBL" + firstLabel); }
}

int primmaryElseIf(int label) : {
    int elseLabel;
} {
    <ELIF> {
        elseLabel = getNewLabel();
        System.out.println("\tvea LBL" + elseLabel);
        System.out.println("LBL" + label);
    }
    expression() {
        label = getNewLabel();
        System.out.println("\tsifalsovea LBL" + label);
    }
    primmaryThen()
    { return label; }
}

void primmaryUnless() : {
    int label;
} {
    <UNLESS>
    expression() {
        label = getNewLabel();
        System.out.println("\tsiciertovea LBL" + label);
    }
    primmaryThen()
    (primmaryElse(label))?
    <END> { System.out.println("LBL" + label); }
}

void primmaryUntil() : {
    int firstLabel, lastLabel;
} {
    <UNTIL> {
        firstLabel = getNewLabel();
        System.out.println("LBL" + firstLabel);
    }
    expression()
    lastLabel = primmaryDo(true)
    <END> {
        System.out.println("\tvea LBL" + firstLabel);
        System.out.println("LBL" + lastLabel);
    }
}

void primmaryWhile() : {
    int firstLabel, lastLabel;
} {
    <WHILE> {
        firstLabel = getNewLabel();
        System.out.println("LBL" + firstLabel);
    }
    expression()
    lastLabel = primmaryDo(false)
    <END> {
        System.out.println("\tvea LBL" + firstLabel);
        System.out.println("LBL" + lastLabel);
    }
}

void primmaryPrint() : {} {
    <PRINT>
    expression()  { System.out.println("\tprint"); }
}

void expression() : {} {
    mexpression() (expression2())?
}

void expression2() : {
    String operation;
} {
    (
        <ADDITION> { operation = "sum"; }
    |   <SUBSTRACTION> { operation = "sub"; }
    )
    expression() { System.out.println('\t' + operation); }
}

void mexpression() : {} {
    value() (mexpression2())?
}

void mexpression2() : {
    String operation;
} {
    (
        <MULTIPLICATION> { operation = "mul"; }
    |   <DIVISION> { operation = "div"; }
    )
    mexpression() { System.out.println('\t' + operation); }
}

void value() : {
    Token value;
} {
    value = <NUMBER> { System.out.println("\tmete " + value.image); }
|   value = <VARIABLE> { System.out.println("\tvalord " + value.image); }
|   <PARENTHESIS_START> expression() <PARENTHESIS_END>
}